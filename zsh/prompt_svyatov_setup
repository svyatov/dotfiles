#
# A simple theme that displays relevant, contextual information.
#
# Original author:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Load dependencies.
pmodload 'helper'

# helper: look for a filename up the directory tree
find_up() {
  local name="$1" dir="$PWD"
  while [[ "$dir" != "/" ]]; do
    [[ -e "$dir/$name" ]] && return 0
    dir=${dir:h}
  done
  return 1
}

function prompt_svyatov_current_ruby {
  local version=$(mise current ruby)
  echo "R/$version"
}

function prompt_svyatov_current_nodejs {
  local version
  if find_up bun.lock || find_up bun.lockb; then
    # project is using Bun
    version=$(bun --version 2>/dev/null)
    echo "B/$version"
  elif find_up package-lock.json || find_up yarn.lock || find_up pnpm-lock.yaml; then
    # project is using Node
    version=$(mise current node)
    echo "N/$version"
  else
    # fallback to Bun
    version=$(bun --version 2>/dev/null)
    echo "B/$version"
  fi
}

function prompt_svyatov_current_python {
  local version
  if [[ -n "$VIRTUAL_ENV" ]]; then
    # Activated virtualenv - use its Python
    version=$("$VIRTUAL_ENV/bin/python" --version 2>/dev/null | awk '{print $2}')
  elif [[ -x ".venv/bin/python" ]]; then
    # Local .venv directory (uv/poetry style) - use its Python
    version=$(.venv/bin/python --version 2>/dev/null | awk '{print $2}')
  else
    # Use system/homebrew Python
    version=$(python3 --version 2>/dev/null | awk '{print $2}')
  fi
  echo "P/$version"
}

function prompt_svyatov_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_svyatov_pwd="$MATCH"
    unset MATCH
  else
    _prompt_svyatov_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi
}

function prompt_svyatov_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_svyatov_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_svyatov_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_svyatov_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' ➭'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{yellow}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:clean' format '%F{green}✓%f'
  zstyle ':prezto:module:git:info:dirty' format '%F{red}✗%f'
  zstyle ':prezto:module:git:info:action' format ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{green}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format '%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format '%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{red}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{blue}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{magenta}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{cyan}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{yellow}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{white}◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' $(coalesce "%b" "%p" "%c")%s %C%D' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

  # Define prompts.
  PROMPT='%F{yellow}%T%f %F{cyan}${_prompt_svyatov_pwd}%f${git_info:+${(e)git_info[prompt]}}%(!. %B%F{red}#%f%b.)${editor_info[keymap]} '
  RPROMPT='${editor_info[overwrite]}%(?:: %F{red}⏎%f)${VIM:+" %B%F{green}V%f%b"}${git_info[rprompt]} [%F{magenta}$(prompt_svyatov_current_ruby)%f][%F{cyan}$(prompt_svyatov_current_python)%f][%F{yellow}$(prompt_svyatov_current_nodejs)%f]'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_svyatov_setup "$@"
