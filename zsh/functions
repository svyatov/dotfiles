z() {
    if [[ -n $1 ]]; then
        zeus $@
    else
        zeus start
    fi
}

_directories_list() { find $1 -type d -maxdepth 1 -not -name '.' -not -name '..' -exec basename {} + }

u() {
    if [[ -n $1 ]]; then
        cd ../$1
    else
        cd ..
    fi
}
_u() { reply=("${(@f)$(_directories_list ..)}") }
compctl -M 'm:{a-z}={A-Z}' -K _u u

uu() {
    if [[ -n $1 ]]; then
        cd ../../$1
    else
        cd ../..
    fi
}
_uu() { reply=("${(@f)$(_directories_list ../..)}") }
compctl -M 'm:{a-z}={A-Z}' -K _uu uu

mkcd() { mkdir -p "$1" && cd "$1" }
rncd() { rails new "$1" && cd "$1" }

# git clone & cd (accepts only ssh url format: git@github.com:Svyatov/dotfiles.git)
glcd() {
    local repo=${1##*/}
    local dirname=${repo%\.git}

    if [[ -n $2 ]]; then
        dirname=$2
    fi

    git clone "$1" "$dirname" && cd "$dirname"
}

# Marks
export MARKS_PATH=$HOME/.marks
_makrs_usage_help() {
    echo "You can use following commands:"
    echo "  jump [mark_name] - change directory to the specified mark"
    echo "  mark [mark_name] - create a mark for the current directory with a specified name"
    echo "unmark [mark_name] - remove the specified mark"
    echo " marks             - list all available marks"
}
jump() {
    if [[ -z $1 ]]; then _makrs_usage_help; return 1; fi
    cd -P $MARKS_PATH/$1 2> /dev/null || echo "No such mark: $1"
}
mark() {
    if [[ -z $1 ]]; then _makrs_usage_help; return 1; fi
    if [[ ! -d $MARKS_PATH ]]; then
        mkdir -p $MARKS_PATH;
    fi
    ln -s "$(pwd)" $MARKS_PATH/$1
}
unmark() {
    if [[ -z $1 ]]; then _makrs_usage_help; return 1; fi
    if [[ -L $MARKS_PATH/$1 ]]; then
        rm $MARKS_PATH/$1
    else
        echo "No such mark: $1"
    fi
}
marks() {
    \ls -l $MARKS_PATH | tail -n +2 | sed 's/  / /g' | cut -d' ' -f9- | awk -F ' -> ' '{printf "%-10s -> %s\n", $1, $2}'
}
_marks() {
    if [[ -d $MARKS_PATH ]]; then
        reply=($(\ls $MARKS_PATH))
    else
        reply=()
    fi
}
compctl -K _marks jump
compctl -K _marks unmark

_bin_first() {
    if [[ -x ./bin/$1 ]]; then
        ./bin/$1 ${@:2} # means "skip first element in array"
    else
        $(rvm gemdir)/bin/$1 ${@:2}
    fi
}

rails()  { _bin_first rails  $@ }
rake()   { _bin_first rake   $@ }
cap()    { _bin_first cap    $@ }
bundle() { _bin_first bundle $@ }

_rake_does_task_list_need_generating () {
  if [ ! -f .rake_tasks ]; then return 0;
  else
    accurate=$(stat -f%m .rake_tasks)
    changed=$(stat -f%m Rakefile)
    return $(expr $accurate '>=' $changed)
  fi
}
_rake () {
  if [ -f Rakefile ]; then
    if _rake_does_task_list_need_generating; then
      rake --tasks --quiet | cut -d " " -f 2 > .rake_tasks
    fi
    compadd `cat .rake_tasks`
  fi
}
compdef _rake rake
